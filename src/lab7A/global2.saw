import "gf.cry";

let init_global name = do {
   llvm_alloc_global name; 
   llvm_points_to ( llvm_global name )              
                  ( llvm_global_initializer name );
};

// Setup for global x = 6
let f_setup = do {
   y <- llvm_fresh_var "y" (llvm_int 32);
   init_global "x";
   llvm_execute_func [ llvm_term y ];
   llvm_return (llvm_term {{ 7 + y:[32] }});
};

let g_setup = do {
   z <- llvm_fresh_var "z" (llvm_int 32);
   init_global "x";
   llvm_execute_func [ llvm_term z ];
   llvm_return (llvm_term {{ 8 + z:[32] }});
};

let gf_setup = do {
   z <- llvm_fresh_var "z" (llvm_int 32);
   init_global "x";
   llvm_execute_func [ llvm_term z ];
   llvm_return (llvm_term {{ gf z:[32]  }});
};

let main : TopLevel () = do {
   m <- llvm_load_module "global2.bc";
	prf1 <- llvm_verify m "f" [] true f_setup abc;
	prf2 <- llvm_verify m "g" [] true g_setup abc;
	prf3 <- llvm_verify m "gf" [] true gf_setup abc;	
	print "Done!";
};