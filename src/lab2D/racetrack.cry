// There are a number of fueling stations located at various points around
// a race circuit. Suppose the amount of fuel at each fueling station is
// different but that the total fuel around the circuit is exactly what is
// needed by a race car to make one complete circuit.
//
// Find the point on the circuit from which a race car with an empty tank
// may make one complete circuit without running out of fuel by tanking
// up at every fueling station in the circuit. Assume the tank is large
// enough to hold enough fuel to complete the circuit.

// location of fueling stations is the percentage of total circuit distance
// from the 'starting line'.  Ratios are all less than (ratio 1 1) and
// are progressively increasing.
loc = [ (ratio 2 10), (ratio 3 10), (ratio 45 100), (ratio 49 100),
        (ratio 71 100), (ratio 78 100), (ratio 81 100), (ratio 98 100)]

// amount of fuel at each fueling station is a percentage of total fuel
// in all fueling stations
gas = [ (ratio 1 10), (ratio 13 100), (ratio 1 10), (ratio 15 100),
        (ratio 23 100), (ratio 7 100), (ratio 17 100), (ratio 5 100)]

// State: (fuel_seen, fuel_deficit, best_station_so_far)
s = [(0,0,-1)]#[(if (ps-ss.0 > ss.1) then (ss.0+fs, ps-ss.0, i)
                                     else (ss.0+fs,ss.1,ss.2))
               | fs <- gas | ps <- loc | ss <- s | i <- [0 ...]]

// Answers:
station = (s ! 0).2
station_location = loc @ station

// ps = % of track covered
// ss.0 = % of fuel seen
// ps-ss.0 is the fuel deficit 
